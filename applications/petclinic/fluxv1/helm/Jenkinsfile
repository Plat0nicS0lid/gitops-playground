#!groovy

String getApplication() { "spring-petclinic-helm" }
String getScmManagerCredentials() { 'scmm-user' }
String getConfigRepositoryUrl() { 'scmm-scm-manager:9091/scm/repo/fluxv1/gitops' }
String getConfigRepositoryPRUrl() { 'scmm-scm-manager:9091/scm/api/v2/pull-requests/fluxv1/gitops' }
// The docker daemon cant use the k8s service name, because it is not running inside the cluster
String getDockerRegistryBaseUrl() { "localhost:8999" }
String getCesBuildLibVersion() { '1.44.3' }
String getCesBuildLibRepo() { 'https://github.com/cloudogu/ces-build-lib/' }
String getHelmImage() { 'ghcr.io/cloudogu/helm:3.4.1-1' }
String getHelmChartRepository() { 'scmm-scm-manager.default.svc.cluster.local:9091/scm/repo/common/spring-boot-helm-chart' }
String getHelmChartName() { "springboot" }
String getHelmChartVersion() { "1.0.0" }
String getMainBranch() { 'main' }

cesBuildLib = library(identifier: "ces-build-lib@${cesBuildLibVersion}",
        retriever: modernSCM([$class: 'GitSCMSource', remote: cesBuildLibRepo])
).com.cloudogu.ces.cesbuildlib

properties([
        // Don't run concurrent builds, because the ITs use the same port causing random failures on concurrent builds.
        disableConcurrentBuilds()
])

node {
    mvn = cesBuildLib.MavenWrapper.new(this)

    catchError {

        stage('Checkout') {
            checkout scm
        }

        stage('Build') {
            mvn 'clean package -DskipTests -Dcheckstyle.skip'
            archiveArtifacts artifacts: '**/target/*.jar'
        }

        stage('Test') {
            mvn "test -Dmaven.test.failure.ignore=true -Dcheckstyle.skip"
        }

        stage('Docker') {
            String imageTag = createImageTag()
            imageName = "${dockerRegistryBaseUrl}/${application}:${imageTag}"
            mvn "spring-boot:build-image -DskipTests -Dcheckstyle.skip -Dspring-boot.build-image.imageName=${imageName}"

            if (isBuildSuccessful()) {
                def docker = cesBuildLib.Docker.new(this)
                docker.withRegistry("http://${dockerRegistryBaseUrl}") {
                    def image = docker.image(imageName)
                    image.push()
                }
            } else {
                echo 'Skipping docker push, because build not successful'
            }
        }

        stage('Deploy') {
            if (isBuildSuccessful() && env.BRANCH_NAME in [mainBranch]) {

                def gitopsConfig = [
                        scmmCredentialsId : scmManagerCredentials,
                        scmmConfigRepoUrl : "http://${configRepositoryUrl}",
                        scmmPullRequestUrl: "http://${configRepositoryPRUrl}",
                        helmChart: [
                                repoType : 'GIT',
                                repoUrl  : "http://${helmChartRepository}",
                                chartName: helmChartName,
                                version  : helmChartVersion
                        ]
                ]

                String pushedChanges = pushToConfigRepo(gitopsConfig)
                currentBuild.description = createBuildDescription(pushedChanges, imageName)
            } else {
                echo 'Skipping deploy, because build not successful or not on main branch'
            }
        }
    }

    // Archive Unit and integration test results, if any
    junit allowEmptyResults: true, testResults: '**/target/failsafe-reports/TEST-*.xml,**/target/surefire-reports/TEST-*.xml'
}

String pushToConfigRepo(Map gitopsConfig) {

    def git = cesBuildLib.Git.new(this, scmManagerCredentials)
    def changesOnGitOpsRepo = ''

    // Query and store info about application repo before cloning into gitops repo
    def applicationRepo = GitRepo.create(git)

    // Display that Jenkins made the GitOps commits not the application repo author
    git.committerName = 'Jenkins'
    git.committerEmail = 'jenkins@cloudogu.com'

    def configRepoTempDir = '.configRepoTempDir'

    try {

        dir(configRepoTempDir) {

            git url: gitopsConfig.scmmConfigRepoUrl, branch: mainBranch, changelog: false, poll: false
            git.fetch()

            def repoChanges = new HashSet<String>()
            repoChanges += createApplicationForStageAndPushToBranch 'staging', mainBranch, applicationRepo, git, gitopsConfig

            git.checkoutOrCreate(application)
            repoChanges += createApplicationForStageAndPushToBranch 'production', application, applicationRepo, git, gitopsConfig

            changesOnGitOpsRepo = aggregateChangesOnGitOpsRepo(repoChanges)

            if (changesOnGitOpsRepo) {
                createPullRequest(gitopsConfig)
            }
        }
    } finally {
        sh "rm -rf ${configRepoTempDir}"
    }

    return changesOnGitOpsRepo
}

String createApplicationForStageAndPushToBranch(String stage, String branch, GitRepo applicationRepo, def git, Map gitopsConfig) {

    String commitPrefix = stage == 'staging' ? '[S] ' : ''

    sh "mkdir -p ${stage}/${application}"

    writeFile file: "${stage}/${application}/mergedValues.yaml", text: mergeValues("http://scmm-scm-manager:9091/scm/repo/common/spring-boot-helm-chart", ["${env.WORKSPACE}/k8s/values-${stage}.yaml", "${env.WORKSPACE}/k8s/values-shared.yaml"] as String[])
    updateImage("${stage}/${application}/mergedValues.yaml", imageName)
    writeFile file: "${stage}/${application}/helmRelease.yaml", text: createHelmRelease(gitopsConfig.helmChart, "fluxv1-${stage}", "${stage}/${application}/mergedValues.yaml")

    git.add('.')
    if (git.areChangesStagedForCommit()) {
        git.commit(commitPrefix + createApplicationCommitMessage(git, applicationRepo), applicationRepo.authorName, applicationRepo.authorEmail)

        // If some else pushes between the pull above and this push, the build will fail.
        // So we pull if push fails and try again
        git.pushAndPullOnFailure("origin ${branch}")
        return "${stage} (${git.commitHashShort})"
    } else {
        echo "No changes on gitOps repo for ${stage} (branch: ${branch}). Not committing or pushing."
        return ''
    }
}

void updateImage(String deploymentFilePath, String newImageName) {
    def data = readYaml file: deploymentFilePath
    def image = data.image
    image.name = newImageName
    writeYaml file: deploymentFilePath, data: data, overwrite: true
}

private String aggregateChangesOnGitOpsRepo(changes) {
    // Remove empty
    (changes - '')
    // and concat into string
            .join('; ')
}

String createApplicationCommitMessage(def git, def applicationRepo) {
    String issueIds = (applicationRepo.commitMessage =~ /#\d*/).collect { "${it} " }.join('')

    String[] urlSplit = applicationRepo.repositoryUrl.split('/')
    def repoNamespace = urlSplit[-2]
    def repoName = urlSplit[-1]
    String message = "${issueIds}${repoNamespace}/${repoName}@${applicationRepo.commitHash}"

    return message
}

void createPullRequest(Map gitopsConfig) {
    withCredentials([usernamePassword(credentialsId: gitopsConfig.scmmCredentialsId, passwordVariable: 'GIT_PASSWORD', usernameVariable: 'GIT_USER')]) {

        String script =
                'curl -s -o /dev/null -w "%{http_code}" ' +
                        "-u ${GIT_USER}:${GIT_PASSWORD} " +
                        '-H "Content-Type: application/vnd.scmm-pullRequest+json;v=2" ' +
                        '--data \'{"title": "created by service ' + application + '", "source": "' + application + '", "target": "' + mainBranch + '"}\' ' +
                        gitopsConfig.scmmPullRequestUrl

        // For debugging the quotation of the shell script, just do: echo script
        String http_code = sh returnStdout: true, script: script

        // At this point we could write a mail to the last committer that his commit triggered a new or updated GitOps PR

        echo "http_code: ${http_code}"
        // PR exists if we get 409
        if (http_code != "201" && http_code != "409") {
            unstable 'Could not create pull request'
        }
    }
}

private String createBuildDescription(String pushedChanges, String imageName) {
    String description = ''
    description += "GitOps commits: "

    if (pushedChanges) {
        description += pushedChanges
    } else {
        description += 'No changes'
    }

    description += "\nImage: ${imageName}"

    return description
}

String createImageTag() {
    def git = cesBuildLib.Git.new(this)
    String branch = git.simpleBranchName
    String branchSuffix = ""

    if (!"develop".equals(branch)) {
        branchSuffix = "-${branch}"
    }

    return "${new Date().format('yyyyMMddHHmm')}-${git.commitHashShort}${branchSuffix}"
}

String createHelmRelease(Map helmChart, String namespace, String valuesFile) {
    def values = fileToInlineYaml(valuesFile)
    return """apiVersion: helm.fluxcd.io/v1
kind: HelmRelease
metadata:
  name: ${application}
  namespace: ${namespace}
  annotations:
    fluxcd.io/automated: "false"
spec:
  releaseName: ${application}
  chart:
    git: ${helmChart.repoUrl}
    ref: ${helmChart.version}
    path: .
  valuesContent: |-
    ${values}
"""
}

String fileToInlineYaml(String filePath) {
    String values = ""
    String indent = "    "

    def fileContent = readFile filePath
    values += fileContent.split("\\n").join("\n" + indent)
    return values
}

String mergeValues(String chart, String[] files) {
    String merge = ""
    String _files = ""
    files.each {
        _files += "-f $it "
    }

    sh "git clone ${chart} ${env.WORKSPACE}/spring-boot-helm-chart || true"

    withHelm {
        String script = "helm values ${env.WORKSPACE}/spring-boot-helm-chart ${_files}"
        merge = sh returnStdout: true, script: script
    }

    sh "rm -rf ${env.WORKSPACE}/spring-boot-helm-chart || true"

    return merge
}

void withHelm(Closure body) {
    cesBuildLib.Docker.new(this).image(helmImage)
            .inside("${pwd().equals(env.WORKSPACE) ? '' : "-v ${env.WORKSPACE}:${env.WORKSPACE}"}") {
                body()
            }
}

/** Queries and stores info about current repo and HEAD commit */
class GitRepo {

    static GitRepo create(git) {
        // Constructors can't be used in Jenkins pipelines due to CPS
        // https://www.jenkins.io/doc/book/pipeline/cps-method-mismatches/#constructors
        return new GitRepo(git.commitAuthorName, git.commitAuthorEmail, git.commitHashShort, git.commitMessage, git.repositoryUrl)
    }

    GitRepo(String authorName, String authorEmail, String commitHash, String commitMessage, String repositoryUrl) {
        this.authorName = authorName
        this.authorEmail = authorEmail
        this.commitHash = commitHash
        this.commitMessage = commitMessage
        this.repositoryUrl = repositoryUrl
    }

    final String authorName
    final String authorEmail
    final String commitHash
    final String commitMessage
    final String repositoryUrl
}

def cesBuildLib
def imageName
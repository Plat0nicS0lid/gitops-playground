#!groovy

String getApplication() { "nginx" }
String getCesBuildLibVersion() { '1.44.3' }
String getCesBuildLibRepo() { 'https://github.com/cloudogu/ces-build-lib/' }
String getScmManagerCredentials() { 'scmm-user' }
String getKubectlImage() { 'lachlanevenson/k8s-kubectl:v1.19.3' }
String getConfigRepositoryUrl() { "http://scmm-scm-manager/scm/repo/fluxv1/gitops" }
String getConfigRepositoryPRUrl() { 'http://scmm-scm-manager/scm/api/v2/pull-requests/fluxv1/gitops' }
String getHelmChartRepository() { "https://charts.bitnami.com/bitnami" }
String getHelmChartName() { "nginx" }
String getHelmChartVersion() { "8.0.0" }
String getMainBranch() { 'main' }

node('docker') {
    cesBuildLib = library(identifier: "ces-build-lib@${cesBuildLibVersion}",
            retriever: modernSCM([$class: 'GitSCMSource', remote: cesBuildLibRepo])
    ).com.cloudogu.ces.cesbuildlib

    properties([
            // Keep only the last 10 build to preserve space
            disableConcurrentBuilds()
    ])

    def git = cesBuildLib.Git.new(this)

    // In this chart we don't need to set properties in HelmRelease's "values" field. 
    // We leave this mechanism in place as an example.
    helmValuesFromFile = []

    fileConfigmaps = [
            [name : "index",
             file : "../index.html",
             stage: ["staging", "production"]
            ]
    ]

    timeout(activity: true, time: 30, unit: 'MINUTES') {
        
        catchError {
            
            stage('Checkout') {
                checkout scm
                git.clean('')
            }

            stage('Deploy') {
                if (env.BRANCH_NAME in [mainBranch]) {
                    def gitopsConfig = [
                        scmmCredentialsId: scmManagerCredentials,
                        scmmConfigRepoUrl: configRepositoryUrl,
                        scmmPullRequestUrl: configRepositoryPRUrl
                    ]

                    def helmChart = [
                        repoType: 'HELM',
                        repoUrl: helmChartRepository,
                        chartName: helmChartName,
                        version: helmChartVersion
                    ]

                    String pushedChanges = pushToConfigRepo(gitopsConfig, helmChart)
                    setBuildDescription createBuildDescription(pushedChanges)
                } else {
                    setBuildDescription 'No push to gitOps repo, because neither on develop branch nor "forceDeployStaging" param set'
                }
            }
        }
    }
}

private setBuildDescription(String description) {
    echo "Setting build description: ${description}"
    currentBuild.description = description
}

String pushToConfigRepo(Map gitopsConfig, Map helmChart) {
    
    def git = cesBuildLib.Git.new(this, scmManagerCredentials)
    def changesOnGitOpsRepo = ''

    // Query and store info about application repo before cloning into gitops repo  
    def applicationRepo = GitRepo.create(git)

    git.committerName = 'Jenkins'
    git.committerEmail = 'jenkins@cloudogu.com'

    def configRepoTempDir = '.configRepoTempDir'

    try {
        
        dir(configRepoTempDir) {
            
            git url: gitopsConfig.scmmConfigRepoUrl, branch: mainBranch, changelog: false, poll: false
            git.fetch()

            def repoChanges = new HashSet<String>()
            repoChanges += createApplicationForStageAndPushToBranch 'staging', mainBranch, applicationRepo, git, gitopsConfig, helmChart

            git.checkoutOrCreate(application)
            repoChanges += createApplicationForStageAndPushToBranch 'production', application, applicationRepo, git, gitopsConfig, helmChart

            changesOnGitOpsRepo = aggregateChangesOnGitOpsRepo(repoChanges)

            if (changesOnGitOpsRepo) {
                createPullRequest(gitopsConfig)
            }
        }
    } finally {
        sh "rm -rf ${configRepoTempDir}"
    }

    return changesOnGitOpsRepo
}

String createApplicationForStageAndPushToBranch(String stage, String branch, GitRepo applicationRepo, def git, Map gitOpsConfig, Map helmChart) {
    
    String commitPrefix = stage == 'staging' ? '[S] ' : ''

    sh "mkdir -p ${stage}/${application}"
    // copy extra resources like sealed secrets
    echo "Copying extra resources from application repo to gitOps Repo: 'k8s/${stage}/*' to '${stage}/${application}'"
    sh "cp ${env.WORKSPACE}/k8s/${stage}/*.yaml fluxv1-${stage}/${application}/ || true"

    writeFile file: "${stage}/${application}/helmRelease.yaml", text: createHelmRelease(helmChart, "fluxv1-${stage}", createFromFileValues(stage))
    writeFile file: "${stage}/${application}/valuesMap.yaml", text: createConfigMap("values.yaml", "${env.WORKSPACE}/k8s/values-${stage}.yaml", "${application}-helm-operator-values", "fluxv1-${stage}")

    writeFile file: "${stage}/${application}/sharedValuesMap.yaml", text: createConfigMap("values.yaml", "${env.WORKSPACE}/k8s/values-shared.yaml", "${application}-shared-helm-operator-values", "fluxv1-${stage}")

    creatFileConfigmaps(stage)

    git.add('.')
    if (git.areChangesStagedForCommit()) {
        git.commit(commitPrefix + createApplicationCommitMessage(git, applicationRepo), applicationRepo.authorName, applicationRepo.authorEmail)

        // If some else pushes between the pull above and this push, the build will fail.
        // So we pull if push fails and try again
        git.pushAndPullOnFailure("origin ${branch}")
        return "${stage} (${git.commitHashShort})"
    } else {
        echo "No changes on gitOps repo for ${stage} (branch: ${branch}). Not committing or pushing."
        return ''
    }
}

private String aggregateChangesOnGitOpsRepo(changes) {
    // Remove empty
    (changes - '')
    // and concat into string
            .join('; ')
}

String createApplicationCommitMessage(def git, def applicationRepo) {
    String issueIds = (applicationRepo.commitMessage =~ /#\d*/).collect { "${it} " }.join('')

    String[] urlSplit = applicationRepo.repositoryUrl.split('/')
    def repoNamespace = urlSplit[-2]
    def repoName = urlSplit[-1]
    String message = "${issueIds}${repoNamespace}/${repoName}@${applicationRepo.commitHash}"
    List additions = []

    if (!additions.isEmpty()) {
        // Highlight commits that ran with non-default parameters
        message = "* ${message}\n\n${additions.join('\n')}"
    }

    return message
}

void createPullRequest(Map gitopsConfig) {
    withCredentials([usernamePassword(credentialsId: gitopsConfig.scmmCredentialsId, passwordVariable: 'GIT_PASSWORD', usernameVariable: 'GIT_USER')]) {
        echo 'creating pull request'
        String script =
                'curl -s -o /dev/null -w "%{http_code}" ' +
                        "-u ${GIT_USER}:${GIT_PASSWORD} " +
                        '-H "Content-Type: application/vnd.scmm-pullRequest+json;v=2" ' +
                        '--data \'{"title": "created by service ' + application + '", "source": "' + application + '", "target": "' + mainBranch + '"}\' ' +
                        gitopsConfig.scmmPullRequestUrl

        // For debugging the quotation of the shell script, just do: echo script
        String http_code = sh returnStdout: true, script: script

        // At this point we could write a mail to the last committer that his commit triggered a new or updated GitOps PR

        echo "http_code: ${http_code}"
        // PR exists if we get 409
        if (http_code != "201" && http_code != "409") {
            unstable 'Could not create pull request'
        }
    }
}

private String createBuildDescription(String pushedChanges) {
    String description = ''

    description += "${description ? '\n' : ''}GitOps commits: "

    if (pushedChanges) {
        description += pushedChanges
    } else {
        description += 'No changes'
    }

    return description
}

String createHelmRelease(Map helmChart, String namespace, String extraValues) {
    return """apiVersion: helm.fluxcd.io/v1
kind: HelmRelease
metadata:
  name: ${application}
  namespace: ${namespace}
  annotations:
    fluxcd.io/automated: "false"
spec:
  releaseName: ${application}
  chart:
    repository: ${helmChart.repoUrl}
    name: ${helmChart.chartName}
    version: ${helmChart.version}
    
  valuesFrom:
  - configMapKeyRef:
      name: ${application}-shared-helm-operator-values
      namespace: ${namespace}
      key: values.yaml
      optional: false
  - configMapKeyRef:
      name: ${application}-helm-operator-values
      namespace: ${namespace}
      key: values.yaml
      optional: false
"""
}

String createFromFileValues(String stage) {
    String values = ""

    helmValuesFromFile.each {
        if (stage in it['stage']) {
            values = fileToInlineYaml(it['key'], "${env.WORKSPACE}/k8s/${it['file']}")
        }
    }
    return values
}

void creatFileConfigmaps(String stage) {
    fileConfigmaps.each {
        if(stage in it['stage']) {
            String key = it['file'].split('/').last()
            writeFile file: "${stage}/${application}/${it['name']}.yaml", text: createConfigMap(key, "${env.WORKSPACE}/k8s/${it['file']}", it['name'], "fluxv1-${stage}")
        }
    }
}

String createConfigMap(String key, String filePath, String name, String namespace) {
    String configMap = ""
    withKubectl {
        String script = "KUBECONFIG=${writeKubeConfig()} kubectl create configmap ${name} " +
                "--from-file=${key}=${filePath} " +
                "--dry-run=client -o yaml -n ${namespace}"

        configMap = sh returnStdout: true, script: script
    }
    return configMap
}

// Dummy kubeConfig, so we can use `kubectl --dry-run=client`
String writeKubeConfig() {
    String kubeConfigPath = "${pwd()}/.kube/config"
    echo "Writing $kubeConfigPath"
    writeFile file: kubeConfigPath, text: """apiVersion: v1
clusters:
- cluster:
    certificate-authority-data: DATA+OMITTED
    server: https://localhost
  name: self-hosted-cluster
contexts:
- context:
    cluster: self-hosted-cluster
    user: svcs-acct-dply
  name: svcs-acct-context
current-context: svcs-acct-context
kind: Config
preferences: {}
users:
- name: svcs-acct-dply
  user:
    token: DATA+OMITTED"""

    return kubeConfigPath
}

void withKubectl(Closure body) {
    cesBuildLib.Docker.new(this).image(kubectlImage)
    // Allow accessing WORKSPACE even when we are in a child dir (using "dir() {}")
            .inside("${pwd().equals(env.WORKSPACE) ? '' : "-v ${env.WORKSPACE}:${env.WORKSPACE}"}") {
                body()
            }
}

String fileToInlineYaml(String key, String filePath) {
    String values = ""
    String indent = "        "

    def fileContent = readFile filePath
    values += "\n    ${key}: |\n${indent}"
    values += fileContent.split("\\n").join("\n" + indent)

    return values
}

/** Queries and stores info about current repo and HEAD commit */
class GitRepo {

    static GitRepo create(git) {
        // Constructors can't be used in Jenkins pipelines due to CPS
        // https://www.jenkins.io/doc/book/pipeline/cps-method-mismatches/#constructors
        return new GitRepo(git.commitAuthorName, git.commitAuthorEmail, git.commitHashShort, git.commitMessage, git.repositoryUrl)
    }

    GitRepo(String authorName, String authorEmail, String commitHash, String commitMessage, String repositoryUrl) {
        this.authorName = authorName
        this.authorEmail = authorEmail
        this.commitHash = commitHash
        this.commitMessage = commitMessage
        this.repositoryUrl = repositoryUrl
    }

    final String authorName
    final String authorEmail
    final String commitHash
    final String commitMessage
    final String repositoryUrl
}

def cesBuildLib